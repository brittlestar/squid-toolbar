; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=Squid
AppVerName=Squid
AppPublisher=Brittlestar
AppPublisherURL=http://www.brittlestar.fr
AppSupportURL=http://www.brittlestar.fr
AppUpdatesURL=http://www.brittlestar.fr
DefaultDirName={pf}\Squid
DefaultGroupName=Squid
DisableDirPage=yes
DisableStartupPrompt=yes
DisableProgramGroupPage=yes
OutPutDir=..\CdTree
OutputBaseFilename=SquidSetup
AppCopyright=Copyright Brittlestar, 2007
PrivilegesRequired=admin
SetupIconFile=squid.ico
UninstallDisplayIcon={app}\Squid.exe
ShowLanguageDialog=yes
VersionInfoVersion=1.4.0.0

[Files]
Source: ..\Build\Squid.exe; DestDir: {app}; Flags: restartreplace; MinVersion: 0,5.01.2600;
Source: ..\Build\Enjoy.exe; DestDir: {app}; Flags: restartreplace;
Source: ..\Build\SquidHook.dll; DestDir: {app}; Flags: restartreplace uninsrestartdelete;

[Run]
Filename: {app}\Squid.exe; Flags: nowait; Parameters: en; Languages: en; OnlyBelowVersion: 0,6;
Filename: {app}\Squid.exe; Flags: nowait; Parameters: fr; Languages: fr; OnlyBelowVersion: 0,6;
Filename: {app}\Enjoy.exe; Flags: nowait; Parameters: en; Languages: en; OnlyBelowVersion: 0,6;
Filename: {app}\Enjoy.exe; Flags: nowait; Parameters: fr; Languages: fr; OnlyBelowVersion: 0,6;

[UninstallDelete]
Type: files; Name: {userappdata}\Squid.ini;
Type: filesandordirs; Name: {app}

[Icons]
Name: "{group}\Squid"; Filename: {app}\Squid.exe; Parameters: en; Languages: en
Name: "{group}\Squid"; Filename: {app}\Squid.exe; Parameters: fr; Languages: fr
Name: "{group}\Squid Help Wizard"; Filename: {app}\Enjoy.exe; Parameters: en; Languages: en
Name: "{group}\Squid Aide Pas-a-Pas"; Filename: {app}\Enjoy.exe; Parameters: fr; Languages: fr
Name: "{group}\Uninstall"; Filename: {app}\{uninstallexe};
Name: "{userstartup}\Squid"; Filename: {app}\Squid.exe; Parameters: en; Languages: en
Name: "{userstartup}\Squid"; Filename: {app}\Squid.exe; Parameters: fr; Languages: fr

[Languages]
Name: en; MessagesFile: English.isl; LicenseFile: license_en.rtf;
Name: fr; MessagesFile: French.isl; LicenseFile: license_fr.rtf;

[Code]
procedure Stop;
var hWnd_L: LongInt;
begin
  if CheckForMutexes('BrittlestarSQUID') then
  begin
    hWnd_L := FindWindowByWindowName('Squid');
    if hWnd_L <> 0 then
    begin
      SendMessage(hWnd_L, $0400 + 2, 0, 0);
    end;
  end;
end;

function InitializeSetup(): Boolean;
begin
  Stop;
  Result := true;
end;

function InitializeUninstall(): Boolean;
begin
  Stop;
  Result := true;
end;

function UninstallNeedRestart(): Boolean;
begin
  RestartReplace(ExpandConstant('{app}\SquidHook.dll'), '');
  Result := true;
end;

function NeedRestart: Boolean;
var Version: TWindowsVersion;
begin
  Result := false;
  GetWindowsVersionEx(Version);
  if Version.Major >= 6 then
    Result := true;
end;

